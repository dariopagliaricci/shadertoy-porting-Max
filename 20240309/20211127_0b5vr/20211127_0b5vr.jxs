<jittershader name="default">
	<description>Default Slab </description>

	<param name="MVP" type="mat4" state="MODELVIEW_PROJECTION_MATRIX" />

	<param name="pos" type="vec3" state="POSITION" />
	<param name="uv" type="vec2" state="TEXCOORD" />

	<param name="viewPort" type="vec2" state="VIEWPORT" />
	<param name="iTime" type="float" default="0.0" />
  <param name="iChannel0" type="int" default="0" />


	<language name="glsl" version="1.5">

		<bind param="MVP" program="vp" />

		<bind param="pos" program="vp" />
		<bind param="uv" program="vp" />

		<bind param="viewPort" program="fp" />
		<bind param="iTime" program="fp" />
    <bind param="iChannel0" program="fp" />


		<program name="vp" type="vertex"  >
		<![CDATA[
			#version 330 core
			
			in vec3 pos;
			in vec2 uv;

			out jit_PerVertex {
				vec2 uv;
			} jit_out;
			uniform mat4 MVP;

			
			void main(void) {
				gl_Position = MVP*vec4(pos, 1.);
				jit_out.uv = uv;

			}
		]]>
		</program>
		
		<program name="fp" type="fragment"  >
		<![CDATA[
#version 330 core

in jit_PerVertex {
	vec2 uv;

} jit_in;
layout (location = 0) out vec4 outColor;

uniform vec2 viewPort;
uniform float iTime;
uniform sampler2D iChannel0;

#define fs(i) (fract(sin((i)*114.514)*1919.810))
#define lofi(i,j) (floor((i)/(j))*(j))

const float PI=acos(-1.);
const float TAU=PI*2.;

float time;
float seed;

float random(){
  seed=fs(seed);
  return seed;
}

mat3 orthBas(vec3 z){
  z=normalize(z);
  vec3 up=abs(z.y)>.99?vec3(0,0,1):vec3(0,1,0);
  vec3 x=normalize(cross(up,z));
  return mat3(x,cross(z,x),z);
}

mat2 r2d(float t){
  return mat2(cos(t),sin(t),-sin(t),cos(t));
}

vec3 sampleLambert(vec3 n){
  float phi16=TAU*random();
  float ct=sqrt(random());
  float st=sqrt(1.0-ct*ct);
  return orthBas(n)*vec3(
    cos(phi16)*st,
    sin(phi16)*st,
    ct
  );
}

vec4 ibox(vec3 ro,vec3 rd,vec3 s){
  vec3 src=ro/rd;
  vec3 dst=abs(s/rd);
  vec3 fv=-src-dst;
  vec3 bv=-src+dst;
  float f=max(max(fv.x,fv.y),fv.z);
  float b=min(min(bv.x,bv.y),bv.z);
  if(f<0.||b<f){return vec4(1E2);}
  vec3 n=-sign(rd)*step(fv.zxy,fv)*step(fv.yzx,fv);
  return vec4(n,f);
}

struct QTR{
  vec3 cell;
  vec3 size;
  float len;
  bool hole;
};

QTR qt(vec3 ro,vec3 rd){
  QTR r;
  r.size=vec3(1,1E3,1);
  for(int i=0;i<4;i++){
    r.size/=2.;
    r.cell=lofi(ro+rd*1E-2*r.size,r.size)+r.size/2.;
    float d1=fs(dot(vec3(.2,1.4,-2.),r.cell));
    r.hole=(
      r.cell.y>0.
      || d1>.8
    );
    if(r.hole){break;}
    float d2=fs(dot(vec3(4,5,6),r.cell));
    if(d2>.5){break;}
  }
  
  
  vec3 src=(ro-r.cell)/rd;
  vec3 dst=abs(r.size/2./rd);
  vec3 bv=-src+dst;
  float b=min(min(bv.x,bv.y),bv.z);
  r.len=b;
  
  return r;
}

void main()
{
  vec2 fragCoord = jit_in.uv * viewPort.xy;
  vec2 uv = vec2(fragCoord.x / viewPort.x, fragCoord.y / viewPort.y);
  vec2 p=uv*2.-1.;
  p.x*=viewPort.x/viewPort.y;
  
  time=iTime;
  seed=texture(iChannel0,uv*8.).x;
  seed+=fract(time);
  
  vec3 col=vec3(0);
  vec3 colRem=vec3(1);
  
  p.xy*=r2d(.4);
  
  vec3 co=vec3(0,1,2);
  co.zx*=r2d(.2*time);
  vec3 ct=vec3(.2,-1,0);
  vec3 cz=normalize(co-ct);
  
  vec3 ro=co;
  vec3 ro0=ro;
  vec3 rd=orthBas(cz)*normalize(vec3(p,-2));
  vec3 rd0=rd;
  
  bool shouldInit=true;
  float samples=0.;
  
  for(int i=0;i<99;i++){
    if(shouldInit){
      shouldInit=false;
      ro=ro0;
      rd=rd0;
      colRem=vec3(1);
      samples++;
    }
    
    QTR qtr=qt(ro,rd);
    
    vec4 isect=vec4(1E2);
    vec3 off=vec3(0);
    if(!qtr.hole){
      off.y-=1.;
      float d1=fs(dot(qtr.cell,vec3(.2,.8,.6)));
      off.y-=sin(d1*6.+time);
      vec3 size=vec3(qtr.size/2.-.02);
      isect=ibox(ro-qtr.cell-off,rd,size);
    }
    
    if(isect.w<1E2){
      ro+=rd*qtr.len; // fuck fuck fuck fuck fuck fuck fuck fuck fuck fuck
      // intended: ro+=rd*isect.w;

      if((ro-off).y>-1.*qtr.size.x){
        col+=colRem*5.;
        colRem*=0.;
      }

      vec3 N=isect.xyz;
      colRem*=.3;
     
      rd=mix(
        sampleLambert(N),
        reflect(rd,N),
        .5
      );
    }else{
      ro+=rd*qtr.len;
    }
    
    if(colRem.x<.01){
      shouldInit=true;
    }
  }
  
  col/=samples;
  col*=1.0-length(p)*.3;
  col=vec3(
    smoothstep(.1,.9,col.x),
    smoothstep(.0,1.,col.y),
    smoothstep(-.1,1.1,col.z)
  );

  outColor = vec4(col,1);
}
]]>
</program>
</language>
</jittershader>
